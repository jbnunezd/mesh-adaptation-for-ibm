!======================================================================================================================!
!
! IBM-MESH-ADAPTATION-TOOL
!
! Copyright (c) 2020 by Jonatan Nunez
!
! This program is free software: you can redistribute it and/or modify it under the terms of the GNU 
! General Public License as published by the Free Software Foundation, either version 3 of the License, 
! or (at your option) any later version.
! 
! This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even 
! the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
! See the GNU General Public License for more details.
! 
! You should have received a copy of the GNU General Public License along with this program.
! If not, see <https://www.gnu.org/licenses/>.
!
!======================================================================================================================!
!
!======================================================================================================================!
#include "main.h"
!======================================================================================================================!
!
!======================================================================================================================!
MODULE MOD_GeometryMainMethods
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GLOBAL_vars
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GeometryMain_vars
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
PUBLIC
!----------------------------------------------------------------------------------------------------------------------!
!
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE CreateFacet
  MODULE PROCEDURE CreateFacet
END INTERFACE

INTERFACE RemoveFacet
  MODULE PROCEDURE RemoveFacet
END INTERFACE

INTERFACE DisconnectFacet
  MODULE PROCEDURE DisconnectFacet
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE CreateFacetNode
  MODULE PROCEDURE CreateFacetNode
END INTERFACE

INTERFACE AddDataToFacetNode
  MODULE PROCEDURE AddDataToFacetNode
END INTERFACE

INTERFACE GetFacetNodeData
  MODULE PROCEDURE GetFacetNodeData
END INTERFACE

INTERFACE RemoveFacetNode
  MODULE PROCEDURE RemoveFacetNode
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE AddFacetToList
  MODULE PROCEDURE AddFacetToList
END INTERFACE

INTERFACE PrintFacetList
  MODULE PROCEDURE PrintFacetList
END INTERFACE

INTERFACE DestructFacetList
  MODULE PROCEDURE DestructFacetList
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE SetUniqueFacetID
  MODULE PROCEDURE SetUniqueFacetID
END INTERFACE

INTERFACE SetUniqueFacetNodes
  MODULE PROCEDURE SetUniqueFacetNodes
END INTERFACE

INTERFACE SetFacetNodesMarkerToZero
  MODULE PROCEDURE SetFacetNodesMarkerToZero
END INTERFACE

INTERFACE SetFacetNodesMarkerToNodeID
  MODULE PROCEDURE SetFacetNodesMarkerToNodeID
END INTERFACE

INTERFACE SaveFacetNodesToNodesList
  MODULE PROCEDURE SaveFacetNodesToNodesList
END INTERFACE

INTERFACE SetFacetNodesToNewFacetNodes
  MODULE PROCEDURE SetFacetNodesToNewFacetNodes
END INTERFACE

INTERFACE SetAndCountNodeID
  MODULE PROCEDURE SetAndCountNodeID
END INTERFACE

INTERFACE CountFacets
  MODULE PROCEDURE CountFacets
END INTERFACE

INTERFACE CountFacetNodes
  MODULE PROCEDURE CountFacetNodes
END INTERFACE

INTERFACE ComparePoints
  MODULE PROCEDURE ComparePoints
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
!
!
!
!======================================================================================================================!
CONTAINS
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreateFacetNode(FacetNode,RefCount)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetNode),POINTER,INTENT(INOUT) :: FacetNode
INTEGER,OPTIONAL,INTENT(IN)            :: RefCount
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

ALLOCATE(FacetNode)
FacetNode%NodeID = 0

IF(PRESENT(RefCount)) THEN
  FacetNode%RefCount = RefCount
ELSE
  FacetNode%RefCount = 0
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreateFacetNode
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreatefFacetNodeAndSetNodeID(FacetNode,NodeID)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetNode),POINTER,INTENT(INOUT) :: FacetNode
INTEGER,OPTIONAL,INTENT(INOUT)         :: NodeID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

CALL CreateFacetNode(FacetNode)

NodeID           = NodeID+1
FacetNode%NodeID = NodeID

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreatefFacetNodeAndSetNodeID
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE AddDataToFacetNode(FacetNode,NodeID,Coords)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetNode),INTENT(INOUT) :: FacetNode
INTEGER,INTENT(IN)             :: NodeID
REAL,INTENT(IN)                :: Coords(1:3)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

FacetNode%NodeID = NodeID
FacetNode%Coords = Coords

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE AddDataToFacetNode
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE GetFacetNodeData(FacetNode,NodeID,Coords)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetNode),INTENT(IN)  :: FacetNode
INTEGER,OPTIONAL,INTENT(OUT) :: NodeID
REAL,OPTIONAL,INTENT(OUT)    :: Coords(1:3)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

IF (PRESENT(NodeID)) THEN
  NodeID = FacetNode%NodeID
END IF

IF (PRESENT(Coords)) THEN
  Coords = FacetNode%Coords
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE GetFacetNodeData
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreateFacet(Facet,FacetNodes,FacetID)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: Facet
TYPE(tFacetNodePtr),INTENT(IN)     :: FacetNodes(1:3)
INTEGER,OPTIONAL,INTENT(IN)        :: FacetID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

ALLOCATE(Facet)
NULLIFY(Facet%PrevFacet)
NULLIFY(Facet%NextFacet)
NULLIFY(Facet%Nodes)

Facet%FacetID = 0

IF (PRESENT(FacetID)) THEN
  Facet%FacetID = FacetID
END IF

ALLOCATE(Facet%Nodes(1:3))
Facet%Nodes(1)%Node => FacetNodes(1)%Node
Facet%Nodes(2)%Node => FacetNodes(2)%Node
Facet%Nodes(3)%Node => FacetNodes(3)%Node

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreateFacet
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE RemoveFacet(Facet,FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: Facet
TYPE(tFacetList),INTENT(INOUT)     :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!

IF (.NOT. ASSOCIATED(Facet)) THEN
  RETURN
END IF

IF (ASSOCIATED(Facet%Nodes)) THEN
  DO iNode=1,3
    CALL RemoveFacetNode(Facet%Nodes(iNode)%Node)
  END DO
  DEALLOCATE(Facet%Nodes)
END IF

CALL DisconnectFacet(Facet,FacetList)

DEALLOCATE(Facet)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE RemoveFacet
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE RemoveFacetNode(FacetNode)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetNode),POINTER,INTENT(INOUT) :: FacetNode
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

IF (.NOT. ASSOCIATED(FacetNode)) THEN
  RETURN
END IF

FacetNode%RefCount = FacetNode%RefCount-1

IF (FacetNode%RefCount .LE. 0) THEN
  NULLIFY(FacetNode)
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE RemoveFacetNode
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE DisconnectFacet(Facet,FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: Facet
TYPE(tFacetList),INTENT(INOUT)     :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

IF (.NOT. ASSOCIATED(Facet)) THEN
  RETURN
END IF

IF (ASSOCIATED(Facet%PrevFacet)) THEN
  Facet%PrevFacet%NextFacet => Facet%NextFacet
ELSE
  FacetList%FirstFacet => Facet%NextFacet
END IF

IF (ASSOCIATED(Facet%NextFacet)) THEN
  Facet%NextFacet%PrevFacet => Facet%PrevFacet
ELSE
  FacetList%LastFacet => Facet%PrevFacet
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE DisconnectFacet
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE DestructFacetList(FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(INOUT) :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER :: aFacet
TYPE(tFacet),POINTER :: bFacet
!----------------------------------------------------------------------------------------------------------------------!

IF (.NOT. ASSOCIATED(FacetList%FirstFacet)) THEN
  RETURN
END IF

aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  bFacet => aFacet%NextFacet
  CALL RemoveFacet(aFacet,FacetList)
  aFacet => bFacet
END DO

NULLIFY(FacetList%FirstFacet)
NULLIFY(FacetList%LastFacet)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE DestructFacetList
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE AddFacetToList(Facet,FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: Facet
TYPE(tFacetList),INTENT(INOUT)     :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

Facet%PrevFacet => FacetList%LastFacet
NULLIFY(Facet%NextFacet)

IF (.NOT. ASSOCIATED(FacetList%LastFacet)) THEN
  FacetList%FirstFacet => Facet
  FacetList%LastFacet  => Facet
ELSE
  FacetList%LastFacet%NextFacet => Facet
END IF

FacetList%LastFacet => Facet

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE AddFacetToList
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE PrintFacetList(FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(INOUT) :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
INTEGER :: FacetID
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER :: aFacet
!----------------------------------------------------------------------------------------------------------------------!

aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  FacetID = aFacet%FacetID
  WRITE(UNIT_SCREEN,'(2X,A,I0)') "FacetID = ", FacetID
  WRITE(UNIT_SCREEN,'(4X,A6,3(2X,A13))') "NodeID", "CoordinateX", "CoordinateY", "CoordinateZ"
  DO iNode=1,3
    WRITE(UNIT_SCREEN,'(4X,I6,3(2X,SP,ES13.6E2))') &
    aFacet%Nodes(iNode)%Node%NodeID, aFacet%Nodes(iNode)%Node%Coords(1:3)
  END DO
  WRITE(UNIT_SCREEN,*)
  aFacet => aFacet%NextFacet
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE PrintFacetList
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetUniqueFacetID(FacetList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(INOUT) :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: FacetID
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER :: aFacet
!----------------------------------------------------------------------------------------------------------------------!

FacetID = 0
aFacet => FacetList%FirstFacet
DO WHILE (ASSOCIATED(aFacet))
  FacetID = FacetID+1
  aFacet%FacetID = FacetID
  aFacet => aFacet%NextFacet
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetUniqueFacetID
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetUniqueFacetNodes(FacetList)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_DataStructures,ONLY: QuickSortArray
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(INOUT) :: FacetList
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
INTEGER :: jNode
INTEGER :: NodeID
INTEGER :: nNodes
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER     :: aFacet
TYPE(tFacetNode),POINTER :: aFacetNode
!----------------------------------------------------------------------------------------------------------------------!
INTEGER                         :: nDeletedNodes
REAL                            :: Point1(1:3)
REAL                            :: Point2(1:3)
REAL,ALLOCATABLE                :: NodesCoordinates(:,:)
TYPE(tFacetNodePtr),ALLOCATABLE :: NodesList(:)
!----------------------------------------------------------------------------------------------------------------------!

! Set marker (tmp) to zero
aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  CALL SetFacetNodesMarkerToZero(aFacet)
  aFacet => aFacet%NextFacet
END DO

! Set marker (tmp) to NodeID
NodeID = 0
aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  CALL SetFacetNodesMarkerToNodeID(aFacet,NodeID)
  aFacet => aFacet%NextFacet
END DO

! Create list of nodes
nNodes = NodeID
ALLOCATE(NodesList(1:nNodes))
DO iNode=1,nNodes
  NULLIFY(NodesList(iNode)%Node)
END DO

! Save all nodes to nodes list
aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  CALL SaveFacetNodesToNodesList(aFacet,NodesList)
  aFacet => aFacet%NextFacet
END DO

! Create nodes coordinates list to remove duplicated nodes
ALLOCATE(NodesCoordinates(1:nNodes,1:4))
DO iNode=1,nNodes
  aFacetNode => NodesList(iNode)%Node
  NodesCoordinates(iNode,1) = aFacetNode%Coords(1)
  NodesCoordinates(iNode,2) = aFacetNode%Coords(2)
  NodesCoordinates(iNode,3) = aFacetNode%Coords(3)
  NodesCoordinates(iNode,4) = REAL(aFacetNode%tmp)
END DO
CALL QuickSortArray(NodesCoordinates,4)

DO iNode=1,nNodes
  NodesList(iNode)%Node%tmp = 0
END DO

NodeID = 0
nDeletedNodes = 0
DO iNode=1,nNodes
  jNode = INT(NodesCoordinates(iNode,4))
  IF (iNode .GT. 1) THEN
    Point1(1:3) = NodesCoordinates(iNode  ,1:3)
    Point2(1:3) = NodesCoordinates(iNode-1,1:3)
    IF (ComparePoints(Point1,Point2) .EQV. .TRUE.) THEN
      nDeletedNodes = nDeletedNodes+1
      NodesCoordinates(iNode,4) = NodesCoordinates(iNode-1,4)
      NodesList(jNode)%Node%tmp    = INT(NodesCoordinates(iNode,4))
      NodesList(jNode)%Node%NodeID = NodeID
      CYCLE
    ELSE
      NodeID = NodeID+1
    END IF
  ELSE
    NodeID = NodeID+1
  END IF
  NodesList(jNode)%Node%tmp    = INT(NodesCoordinates(iNode,4))
  NodesList(jNode)%Node%NodeID = NodeID
END DO

! Nodes point now to nodes with new NodeIDs
aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  CALL SetFacetNodesToNewFacetNodes(aFacet,NodesList)
  aFacet => aFacet%NextFacet
END DO

IF (ALLOCATED(NodesCoordinates)) THEN
  DEALLOCATE(NodesCoordinates)
END IF
IF (ALLOCATED(NodesList)) THEN
  DEALLOCATE(NodesList)
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetUniqueFacetNodes
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetFacetNodesMarkerToZero(aFacet)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: aFacet
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!

DO iNode=1,3
  aFacet%Nodes(iNode)%Node%tmp = 0
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetFacetNodesMarkerToZero
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetFacetNodesMarkerToNodeID(aFacet,NodeID)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GeometryMain_vars,ONLY: tFacet
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: aFacet
INTEGER,INTENT(INOUT)              :: NodeID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!

DO iNode=1,3
  CALL SetAndCountNodeID(aFacet%Nodes(iNode)%Node%tmp,NodeID)
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetFacetNodesMarkerToNodeID
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SaveFacetNodesToNodesList(aFacet,NodesList)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GeometryMain_vars,ONLY: tFacet
USE MOD_GeometryMain_vars,ONLY: tFacetNodePtr
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: aFacet
TYPE(tFacetNodePtr),INTENT(INOUT)  :: NodesList(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!

DO iNode=1,3
  NodesList(aFacet%Nodes(iNode)%Node%tmp)%Node => aFacet%Nodes(iNode)%Node
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SaveFacetNodesToNodesList
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetFacetNodesToNewFacetNodes(aFacet,NodesList)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GeometryMain_vars,ONLY: tFacet
USE MOD_GeometryMain_vars,ONLY: tFacetList
USE MOD_GeometryMain_vars,ONLY: tFacetNodePtr
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER,INTENT(INOUT) :: aFacet
TYPE(tFacetNodePtr),INTENT(INOUT)  :: NodesList(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!

DO iNode=1,3
  aFacet%Nodes(iNode)%Node => NodesList(aFacet%Nodes(iNode)%Node%tmp)%Node
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetFacetNodesToNewFacetNodes
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE SetAndCountNodeID(iNodeID,jNodeID)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,INTENT(INOUT) :: iNodeID
INTEGER,INTENT(INOUT) :: jNodeID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

IF (iNodeID .EQ. 0) THEN
  jNodeID = jNodeID+1
  iNodeID = jNodeID
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE SetAndCountNodeID
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CountFacets(FacetList,nFacets)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(IN) :: FacetList
INTEGER,INTENT(INOUT)       :: nFacets
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER :: aFacet
!----------------------------------------------------------------------------------------------------------------------!

! Compute nFacets
nFacets = 0
aFacet => FacetList%FirstFacet
DO WHILE(ASSOCIATED(aFacet))
  nFacets = nFacets+1
  aFacet => aFacet%NextFacet
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CountFacets
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CountFacetNodes(FacetList,nNodes)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacetList),INTENT(IN) :: FacetList
INTEGER,INTENT(INOUT)       :: nNodes
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iNode
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tFacet),POINTER :: aFacet
!----------------------------------------------------------------------------------------------------------------------!

! Compute nNodes
nNodes = 0
aFacet => FacetList%FirstFacet
DO WHILE (ASSOCIATED(aFacet))
  DO iNode=1,3
    IF (aFacet%Nodes(iNode)%Node%NodeID .GT. nNodes) THEN
      nNodes = aFacet%Nodes(iNode)%Node%NodeID
    END IF
  END DO
  aFacet => aFacet%NextFacet
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CountFacetNodes
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION ComparePoints(Point1,Point2) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: Point1(1:3)
REAL,INTENT(IN) :: Point2(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL            :: ds
!----------------------------------------------------------------------------------------------------------------------!

ds = SQRT(DOT_PRODUCT(Point1-Point2,Point1-Point2))
Flag = .FALSE.
IF (ds .LT. PP_ACCURACY) THEN
  Flag = .TRUE.
END IF

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION ComparePoints
!======================================================================================================================!
!
!
!
!----------------------------------------------------------------------------------------------------------------------!
END MODULE MOD_GeometryMainMethods
!======================================================================================================================!
