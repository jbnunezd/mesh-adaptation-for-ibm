!======================================================================================================================!
!
! IBM-MESH-ADAPTATION-TOOL
!
! Copyright (c) 2020 by Jonatan Nunez
!
! This program is free software: you can redistribute it and/or modify it under the terms of the GNU 
! General Public License as published by the Free Software Foundation, either version 3 of the License, 
! or (at your option) any later version.
! 
! This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even 
! the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
! See the GNU General Public License for more details.
! 
! You should have received a copy of the GNU General Public License along with this program.
! If not, see <https://www.gnu.org/licenses/>.
!
!======================================================================================================================!
!
!======================================================================================================================!
#include "main.h"
!======================================================================================================================!
!
!======================================================================================================================!
MODULE MOD_HashTables
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GLOBAL_vars
USE MOD_BucketsArray
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
PUBLIC
!----------------------------------------------------------------------------------------------------------------------!
!
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE CreateHashTable
  MODULE PROCEDURE CreateHashTable
END INTERFACE

INTERFACE GetDataFromHashTable
  MODULE PROCEDURE GetDataFromHashTable
END INTERFACE

INTERFACE PrintHashTable
  MODULE PROCEDURE PrintHashTable
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE CheckBucketIDIsInHashTable
  MODULE PROCEDURE CheckBucketIDIsInHashTable
END INTERFACE

INTERFACE AddBucketIDToHashTable
  MODULE PROCEDURE AddBucketIDToHashTable
END INTERFACE

INTERFACE GetHashKey
  MODULE PROCEDURE GetHashKey
END INTERFACE

INTERFACE GetBucketID
  MODULE PROCEDURE GetBucketID
END INTERFACE

INTERFACE GetHashTableSize
  MODULE PROCEDURE GetHashTableSize
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
!
!
!
!======================================================================================================================!
CONTAINS
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreateHashTable(HashTable,nBucketsIn,nData)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(INOUT),ALLOCATABLE :: HashTable(:)
INTEGER,INTENT(IN)                      :: nBucketsIn
INTEGER,INTENT(IN)                      :: nData
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: nBuckets
!----------------------------------------------------------------------------------------------------------------------!

! ! ! nBuckets = GetHashTableSize(nBucketsIn) 
nBuckets = nBucketsIn 

CALL CreateBucketsArray(HashTable,nBuckets,nData)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreateHashTable
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE PrintHashTable(HashTable)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(IN) :: HashTable(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iBucket
!----------------------------------------------------------------------------------------------------------------------!

DO iBucket=1,SIZE(HashTable,1)
  CALL PrintBucket(iBucket,HashTable(iBucket))
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE PrintHashTable
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE GetDataFromHashTable(HashTable,Data,ID)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(IN) :: HashTable(:)
INTEGER,INTENT(IN)       :: Data(:)
INTEGER,INTENT(OUT)      :: ID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: HashKey
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),POINTER :: aBucket
!----------------------------------------------------------------------------------------------------------------------!

CALL GetHashKey(Data,SIZE(HashTable),HashKey)

ID = -1
IF (.NOT. ASSOCIATED(HashTable(HashKey)%FirstBucket)) THEN
  RETURN
END IF

aBucket => HashTable(HashKey)%FirstBucket
DO WHILE(ASSOCIATED(aBucket))
  IF (ALL(aBucket%Data .EQ. Data)) THEN
    ID = aBucket%ID
    RETURN
  END IF
  aBucket => aBucket%NextBucket
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE GetDataFromHashTable
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE AddBucketIDToHashTable(HashTable,BucketID,Data)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_ArraySorting,ONLY: QuickSort
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(INOUT) :: HashTable(:)
INTEGER,INTENT(IN)          :: BucketID
INTEGER,INTENT(IN)          :: Data(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: HashKey
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,ALLOCATABLE :: SortedData(:)
!----------------------------------------------------------------------------------------------------------------------!

IF (ALLOCATED(SortedData)) THEN
  DEALLOCATE(SortedData)
END IF
ALLOCATE(SortedData(1:SIZE(Data)))

SortedData = Data
CALL QuickSort(SortedData)
CALL GetHashKey(SortedData,SIZE(HashTable),HashKey)
CALL AddDataToBucket(HashTable(HashKey),BucketID,SortedData)

DEALLOCATE(SortedData)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE AddBucketIDToHashTable
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION GetBucketID(HashTable,Data) RESULT(BucketID)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_ArraySorting,ONLY: QuickSort
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(IN) :: HashTable(:)
INTEGER,INTENT(IN)       :: Data(:)
INTEGER                  :: BucketID
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,ALLOCATABLE :: SortedData(:)
!----------------------------------------------------------------------------------------------------------------------!

ALLOCATE(SortedData(1:SIZE(Data)))

SortedData = Data
CALL QuickSort(SortedData)
CALL GetDataFromHashTable(HashTable,SortedData,BucketID)

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION GetBucketID
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION CheckBucketIDIsInHashTable(HashTable,Data) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_ArraySorting,ONLY: QuickSort
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(IN) :: HashTable(:)
INTEGER,INTENT(IN)       :: Data(:)
LOGICAL                  :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: BucketID
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,ALLOCATABLE :: SortedData(:)
!----------------------------------------------------------------------------------------------------------------------!

IF (ALLOCATED(SortedData) .EQV. .TRUE.) THEN
  DEALLOCATE(SortedData)
END IF
ALLOCATE(SortedData(1:SIZE(Data)))

SortedData = Data
CALL QuickSort(SortedData)
CALL GetDataFromHashTable(HashTable,SortedData,BucketID)

Flag = .FALSE.
IF (BucketID .NE. -1) THEN
  Flag = .TRUE.
END IF

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION CheckBucketIDIsInHashTable
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE GetHashKey(Data,nBuckets,HashKey)
!----------------------------------------------------------------------------------------------------------------------!
USE,INTRINSIC :: ISO_FORTRAN_ENV,ONLY: INT32, INT64
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,INTENT(IN)  :: Data(:)
INTEGER,INTENT(IN)  :: nBuckets
INTEGER,INTENT(OUT) :: HashKey
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iData
INTEGER :: PrimeNumber
!----------------------------------------------------------------------------------------------------------------------!
INTEGER(INT64) :: Key
INTEGER(INT64) :: TableSize
INTEGER(INT64) :: aData(1:SIZE(Data(:)))
!----------------------------------------------------------------------------------------------------------------------!

aData = INT(Data,KIND=INT64)
TableSize = INT(nBuckets,KIND=INT64)

PrimeNumber = 31
Key = 0
DO iData=1,SIZE(Data(:))
  Key = Key + aData(iData)*(PrimeNumber**(iData-1))
END DO

IF (Key .LT. 0) THEN
  WRITE(UNIT_SCREEN,*) "MOD_HashTables::GetHashKey"
  WRITE(UNIT_SCREEN,*) Key
  WRITE(UNIT_SCREEN,*) aData
  WRITE(UNIT_SCREEN,*) TableSize
  STOP
END IF

Key = 1 + MOD(Key,TableSize)
HashKey = INT(Key,KIND=INT32)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE GetHashKey
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION GetHashTableSize(nBucketsIn) RESULT(nBucketsOut)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,INTENT(IN) :: nBucketsIn
INTEGER            :: nBucketsOut
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

nBucketsOut = 4

DO WHILE (nBucketsOut .LT. nBucketsIn)
  nBucketsOut = 2*nBucketsOut
END DO

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION GetHashTableSize
!======================================================================================================================!
!
!
!
!----------------------------------------------------------------------------------------------------------------------!
END MODULE MOD_HashTables
!======================================================================================================================!
