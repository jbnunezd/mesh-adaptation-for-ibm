!======================================================================================================================!
!
! IBM-MESH-ADAPTATION-TOOL
!
! Copyright (c) 2020 by Jonatan Nunez
!
! This program is free software: you can redistribute it and/or modify it under the terms of the GNU 
! General Public License as published by the Free Software Foundation, either version 3 of the License, 
! or (at your option) any later version.
! 
! This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even 
! the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
! See the GNU General Public License for more details.
! 
! You should have received a copy of the GNU General Public License along with this program.
! If not, see <https://www.gnu.org/licenses/>.
!
!======================================================================================================================!
!
!======================================================================================================================!
#include "main.h"
!======================================================================================================================!
!
!======================================================================================================================!
MODULE MOD_BucketsArray
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GLOBAL_vars
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
PUBLIC
!----------------------------------------------------------------------------------------------------------------------!
! GLOBAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
TYPE tBucket
  INTEGER               :: ID
  INTEGER,ALLOCATABLE   :: Data(:)
  TYPE(tBucket),POINTER :: FirstBucket
  TYPE(tBucket),POINTER :: LastBucket
  TYPE(tBucket),POINTER :: NextBucket
END TYPE tBucket
!----------------------------------------------------------------------------------------------------------------------!
!
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE CreateBucket
  MODULE PROCEDURE CreateBucket
END INTERFACE

INTERFACE CreateBucketsArray
  MODULE PROCEDURE CreateBucketsArray
END INTERFACE

INTERFACE AddDataToBucket
  MODULE PROCEDURE AddDataToBucket
END INTERFACE

INTERFACE PrintBucket
  MODULE PROCEDURE PrintBucket
END INTERFACE

INTERFACE PrintBucketsArray
  MODULE PROCEDURE PrintBucketsArray
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
!
!
!
!======================================================================================================================!
CONTAINS
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreateBucket(Bucket,nData)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(INOUT) :: Bucket
INTEGER,INTENT(IN),OPTIONAL :: nData
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!

Bucket%ID   = 0
IF (PRESENT(nData)) THEN
  ALLOCATE(Bucket%Data(1:nData))
  Bucket%Data = 0
END IF

NULLIFY(Bucket%FirstBucket)
NULLIFY(Bucket%LastBucket)
NULLIFY(Bucket%NextBucket)

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreateBucket
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE CreateBucketsArray(BucketsArray,nBuckets,nData)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(INOUT),ALLOCATABLE :: BucketsArray(:)
INTEGER,INTENT(IN)                      :: nBuckets
INTEGER,INTENT(IN),OPTIONAL             :: nData
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iBucket
!----------------------------------------------------------------------------------------------------------------------!

IF (ALLOCATED(BucketsArray)) THEN
  DEALLOCATE(BucketsArray)
END IF
ALLOCATE(BucketsArray(1:nBuckets))

IF (PRESENT(nData)) THEN
  DO iBucket=1,nBuckets
    CALL CreateBucket(BucketsArray(iBucket),nData)
  END DO
ELSE
  DO iBucket=1,nBuckets
    CALL CreateBucket(BucketsArray(iBucket))
  END DO
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE CreateBucketsArray
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE AddDataToBucket(Bucket,ID,Data)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(INOUT) :: Bucket
INTEGER,INTENT(IN)          :: ID
INTEGER,INTENT(IN),OPTIONAL :: Data(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),POINTER :: aBucket
!----------------------------------------------------------------------------------------------------------------------!

ALLOCATE(aBucket)
IF (.NOT. ASSOCIATED(Bucket%LastBucket)) THEN
  Bucket%FirstBucket => aBucket
  Bucket%LastBucket  => aBucket
ELSE
  Bucket%LastBucket%NextBucket => aBucket
  Bucket%LastBucket            => aBucket
END IF
NULLIFY(Bucket%LastBucket%NextBucket)

Bucket%LastBucket%ID = ID
IF (PRESENT(Data)) THEN
  ALLOCATE(aBucket%Data(1:SIZE(Data)))
  Bucket%LastBucket%Data = Data
END IF

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE AddDataToBucket
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE PrintBucket(iBucket,Bucket)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
INTEGER,INTENT(IN)       :: iBucket
TYPE(tBucket),INTENT(IN) :: Bucket
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: nData
!----------------------------------------------------------------------------------------------------------------------!
CHARACTER(LEN=256) :: FormatString
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),POINTER :: aBucket
!----------------------------------------------------------------------------------------------------------------------!

IF (.NOT. ASSOCIATED(Bucket%FirstBucket)) THEN
  RETURN
END IF

aBucket => Bucket%FirstBucket

IF (ALLOCATED(aBucket%Data)) THEN
  WRITE(UNIT_SCREEN,'(2X,A,1X,I8)',ADVANCE='YES') "Bucket:", iBucket
ELSE
  WRITE(UNIT_SCREEN,'(2X,A,1X,I8,1X,A,1X)',ADVANCE='NO') "Bucket:", iBucket, ":"
END IF

DO WHILE(ASSOCIATED(aBucket))
  IF (ALLOCATED(aBucket%Data)) THEN
    nData = SIZE(aBucket%Data)
    WRITE(FormatString,'(A,I0,A)') '(I8,A1,', nData, '(2X,I8))'
    WRITE(UNIT_SCREEN,FormatString,ADVANCE='YES') aBucket%ID,":", aBucket%Data(1:nData)
  ELSE
    nData = 0
    WRITE(FormatString,'(A,I0,A)') '(', nData+1, '(2X,I8))'
    IF (ASSOCIATED(aBucket%NextBucket)) THEN
      WRITE(UNIT_SCREEN,FormatString,ADVANCE='NO') aBucket%ID
    ELSE
      WRITE(UNIT_SCREEN,FormatString,ADVANCE='YES') aBucket%ID
    END IF
  END IF
  aBucket => aBucket%NextBucket
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE PrintBucket
!======================================================================================================================!
!
!
!
!======================================================================================================================!
SUBROUTINE PrintBucketsArray(BucketList)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
TYPE(tBucket),INTENT(IN) :: BucketList(:)
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: iBucket
!----------------------------------------------------------------------------------------------------------------------!

DO iBucket=1,SIZE(BucketList,1)
  CALL PrintBucket(iBucket,BucketList(iBucket))
END DO

!----------------------------------------------------------------------------------------------------------------------!
END SUBROUTINE PrintBucketsArray
!======================================================================================================================!
!
!
!
!----------------------------------------------------------------------------------------------------------------------!
END MODULE MOD_BucketsArray
!======================================================================================================================!
