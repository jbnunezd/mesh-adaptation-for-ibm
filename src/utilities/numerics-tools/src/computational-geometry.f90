 
!======================================================================================================================!
!
! IBM-MESH-ADAPTATION-TOOL
!
! Copyright (c) 2020 by Jonatan Nunez
!
! This program is free software: you can redistribute it and/or modify it under the terms of the GNU 
! General Public License as published by the Free Software Foundation, either version 3 of the License, 
! or (at your option) any later version.
! 
! This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even 
! the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
! See the GNU General Public License for more details.
! 
! You should have received a copy of the GNU General Public License along with this program.
! If not, see <https://www.gnu.org/licenses/>.
!
!======================================================================================================================!
!
!======================================================================================================================!
#include "main.h"
!======================================================================================================================!
!
!======================================================================================================================!
MODULE MOD_ComputationalGeometry
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_GLOBAL_vars
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
PRIVATE
!----------------------------------------------------------------------------------------------------------------------!
INTERFACE PlaneBoxOverlap
  MODULE PROCEDURE PlaneBoxOverlap
END INTERFACE

INTERFACE TriangleBoxOverlap
  MODULE PROCEDURE TriangleBoxOverlap
END INTERFACE

INTERFACE PointIsInsideCube2D
  MODULE PROCEDURE PointIsInsideCube2D
END INTERFACE

INTERFACE PointIsInsideCube3D
  MODULE PROCEDURE PointIsInsideCube3D
END INTERFACE
!----------------------------------------------------------------------------------------------------------------------!
PUBLIC :: PlaneBoxOverlap
PUBLIC :: TriangleBoxOverlap
PUBLIC :: PointIsInsideCube2D
PUBLIC :: PointIsInsideCube3D
!----------------------------------------------------------------------------------------------------------------------!
!
!
!
!======================================================================================================================!
CONTAINS
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION PointIsInsideCube2D(Point,Box) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: Point(1:2)
REAL,INTENT(IN) :: Box(1:4,1:2)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: BoundingBox(1:2,1:2)
!----------------------------------------------------------------------------------------------------------------------!

BoundingBox(1,1) = MINVAL(Box(1:4,1))
BoundingBox(1,2) = MINVAL(Box(1:4,2))
BoundingBox(2,1) = MAXVAL(Box(1:4,1))
BoundingBox(2,2) = MAXVAL(Box(1:4,2))

Flag = Point(1) .GT. BoundingBox(1,1) .AND. Point(1) .LT. BoundingBox(2,1) .AND. &
       Point(2) .GT. BoundingBox(1,2) .AND. Point(2) .LT. BoundingBox(2,2)

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION PointIsInsideCube2D
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION PointIsInsideCube3D(Point,Box) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: Point(1:3)
REAL,INTENT(IN) :: Box(1:8,1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: BoundingBox(1:2,1:3)
!----------------------------------------------------------------------------------------------------------------------!

BoundingBox(1,1) = MINVAL(Box(1:8,1))
BoundingBox(1,2) = MINVAL(Box(1:8,2))
BoundingBox(1,3) = MINVAL(Box(1:8,3))
BoundingBox(2,1) = MAXVAL(Box(1:8,1))
BoundingBox(2,2) = MAXVAL(Box(1:8,2))
BoundingBox(2,3) = MAXVAL(Box(1:8,3))

Flag = Point(1) .GT. BoundingBox(1,1) .AND. Point(1) .LT. BoundingBox(2,1) .AND. &
       Point(2) .GT. BoundingBox(1,2) .AND. Point(2) .LT. BoundingBox(2,2) .AND. &
       Point(3) .GT. BoundingBox(1,3) .AND. Point(3) .LT. BoundingBox(2,3)

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION PointIsInsideCube3D
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION PlaneBoxOverlap(normal,vert,maxbox) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
! 
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_VectorAlgebra
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: normal(1:3)
REAL,INTENT(IN) :: vert(1:3)
REAL,INTENT(IN) :: maxbox(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
INTEGER :: q
REAL    :: v
REAL    :: vmin(1:3)
REAL    :: vmax(1:3)
!----------------------------------------------------------------------------------------------------------------------!

DO q=1,3
  v = vert(q)
  IF (normal(q) .GT. 0.0) THEN
    vmin(q) = -maxbox(q)-v
    vmax(q) = +maxbox(q)-v
  ELSE
    vmin(q) = +maxbox(q)-v
    vmax(q) = -maxbox(q)-v
  END IF
END DO

IF (DOTPRODUCT(normal,vmin) .GT. 0.0) THEN
  Flag = .FALSE.
  RETURN
END IF

IF (DOTPRODUCT(normal,vmax) .GE. 0.0) THEN
  Flag = .TRUE.
  RETURN
END IF

Flag = .FALSE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION PlaneBoxOverlap
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION TriangleBoxOverlap(boxcenter,boxhalfsize,triverts) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
! 
!----------------------------------------------------------------------------------------------------------------------!
USE MOD_VectorAlgebra
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: boxcenter(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
REAL,INTENT(IN) :: triverts(1:3,1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL    :: vmin
REAL    :: vmax
REAL    :: fex
REAL    :: fey
REAL    :: fez
REAL    :: v1(1:3)
REAL    :: v2(1:3)
REAL    :: v3(1:3)
REAL    :: e1(1:3)
REAL    :: e2(1:3)
REAL    :: e3(1:3)
REAL    :: normal(1:3)
!----------------------------------------------------------------------------------------------------------------------!

v1(1:3) = triverts(1,1:3)-boxcenter(1:3)
v2(1:3) = triverts(2,1:3)-boxcenter(1:3)
v3(1:3) = triverts(3,1:3)-boxcenter(1:3)

e1(1:3) = v2(1:3)-v1(1:3)
e2(1:3) = v3(1:3)-v2(1:3)
e3(1:3) = v1(1:3)-v3(1:3)

fex = ABS(e1(1))
fey = ABS(e1(2))
fez = ABS(e1(3))

IF (.NOT. AXISTEST_X01(e1(3),e1(2),fez,fey,v1,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Y02(e1(3),e1(1),fez,fex,v1,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Z12(e1(2),e1(1),fey,fex,v2,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF

fex = ABS(e2(1))
fey = ABS(e2(2))
fez = ABS(e2(3))
IF (.NOT. AXISTEST_X01(e2(3),e2(2),fez,fey,v1,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Y02(e2(3),e2(1),fez,fex,v1,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Z0(e2(2),e2(1),fey,fex,v1,v2,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF

fex = ABS(e3(1))
fey = ABS(e3(2))
fez = ABS(e3(3))
IF (.NOT. AXISTEST_X2(e3(3),e3(2),fez,fey,v1,v2,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Y1(e3(3),e3(1),fez,fex,v1,v2,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF
IF (.NOT. AXISTEST_Z12(e3(2),e3(1),fey,fex,v2,v3,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF

vmin = MIN(v1(1),v2(1),v3(1))
vmax = MAX(v1(1),v2(1),v3(1))
IF ((vmin .GT. boxhalfsize(1)) .OR. (vmax .LT.-boxhalfsize(1))) THEN
  Flag = .FALSE.
  RETURN
END IF

vmin = MIN(v1(2),v2(2),v3(2))
vmax = MAX(v1(2),v2(2),v3(2))
IF ((vmin .GT. boxhalfsize(2)) .OR. (vmax .LT.-boxhalfsize(2))) THEN
  Flag = .FALSE.
  RETURN
END IF

vmin = MIN(v1(3),v2(3),v3(3))
vmax = MAX(v1(3),v2(3),v3(3))
IF ((vmin .GT. boxhalfsize(3)) .OR. (vmax .LT.-boxhalfsize(3))) THEN
  Flag = .FALSE.
  RETURN
END IF

normal(1:3) = CROSSPRODUCT(e1(1:3),e2(1:3))

IF (.NOT. PlaneBoxOverlap(normal,v1,boxhalfsize)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION TriangleBoxOverlap
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_X01(a,b,fa,fb,v1,v3,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v1(1:3)
REAL,INTENT(IN) :: v3(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p1
REAL :: p3
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p1 = a*v1(2)-b*v1(3)
p3 = a*v3(2)-b*v3(3)

IF (p1 .LT. p3) THEN
  min = p1
  max = p3
ELSE
  min = p3
  max = p1
END IF

rad = fa*boxhalfsize(2)+fb*boxhalfsize(3)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_X01
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_X2(a,b,fa,fb,v1,v2,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v1(1:3)
REAL,INTENT(IN) :: v2(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p1
REAL :: p2
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p1 = a*v1(2)-b*v1(3)
p2 = a*v2(2)-b*v2(3)

IF (p1 .LT. p2) THEN
  min = p1
  max = p2
ELSE
  min = p2
  max = p1
END IF

rad = fa*boxhalfsize(2)+fb*boxhalfsize(3)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_X2
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_Y02(a,b,fa,fb,v1,v3,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v1(1:3)
REAL,INTENT(IN) :: v3(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p1
REAL :: p3
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p1 = -a*v1(1)+b*v1(3)
p3 = -a*v3(1)+b*v3(3)

IF (p1 .LT. p3) THEN
  min = p1
  max = p3
ELSE
  min = p3
  max = p1
END IF

rad = fa*boxhalfsize(1)+fb*boxhalfsize(3)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_Y02
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_Y1(a,b,fa,fb,v1,v2,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v1(1:3)
REAL,INTENT(IN) :: v2(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p1
REAL :: p2
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p1 = -a*v1(1)+b*v1(3)
p2 = -a*v2(1)+b*v2(3)

IF (p1 .LT. p2) THEN
  min = p1
  max = p2
ELSE
  min = p2
  max = p1
END IF

rad = fa*boxhalfsize(1)+fb*boxhalfsize(3)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_Y1
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_Z12(a,b,fa,fb,v2,v3,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v2(1:3)
REAL,INTENT(IN) :: v3(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p2
REAL :: p3
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p2 = a*v2(1)-b*v2(2)
p3 = a*v3(1)-b*v3(2)

IF (p3 .LT. p2) THEN
  min = p3
  max = p2
ELSE
  min = p2
  max = p3
END IF

rad = fa*boxhalfsize(1)+fb*boxhalfsize(2)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_Z12
!======================================================================================================================!
!
!
!
!======================================================================================================================!
FUNCTION AXISTEST_Z0(a,b,fa,fb,v1,v2,boxhalfsize) RESULT(Flag)
!----------------------------------------------------------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------!
! FORMAL ARGUMENTS
!----------------------------------------------------------------------------------------------------------------------!
REAL,INTENT(IN) :: a
REAL,INTENT(IN) :: b
REAL,INTENT(IN) :: fa
REAL,INTENT(IN) :: fb
REAL,INTENT(IN) :: v1(1:3)
REAL,INTENT(IN) :: v2(1:3)
REAL,INTENT(IN) :: boxhalfsize(1:3)
LOGICAL         :: Flag
!----------------------------------------------------------------------------------------------------------------------!
! LOCAL VARIABLES
!----------------------------------------------------------------------------------------------------------------------!
REAL :: p1
REAL :: p2
REAL :: min
REAL :: max
REAL :: rad
!----------------------------------------------------------------------------------------------------------------------!

p1 = a*v1(1)-b*v1(2)
p2 = a*v2(1)-b*v2(2)

IF (p1 .LT. p2) THEN
  min = p1
  max = p2
ELSE
  min = p2
  max = p1
END IF

rad = fa*boxhalfsize(1)+fb*boxhalfsize(2)

IF ((min .GT. rad) .OR. (max .LT. -rad)) THEN
  Flag = .FALSE.
  RETURN
END IF

Flag = .TRUE.
RETURN

!----------------------------------------------------------------------------------------------------------------------!
END FUNCTION AXISTEST_Z0
!======================================================================================================================!
!
!
!
!----------------------------------------------------------------------------------------------------------------------!
END MODULE MOD_ComputationalGeometry
!======================================================================================================================!
